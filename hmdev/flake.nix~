{
  description = "My dotfiles HomeManager flake";

  inputs = {
    nixpkgsUnstable.url = "github:NixOS/nixpkgs";
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.05";
    nixpkgsMaster.url = "git+file:///home/steffen/code/nixpkgs?ref=add-darkplaces";
    home.url = "github:nix-community/home-manager/release-23.11";
    home.inputs.nixpkgs.follows = "nixpkgs";
    nur.url = "github:nix-community/nur";
    nixos-hardware.url = "github:nixos/nixos-hardware";
    hyprland.url = "github:hyprwm/Hyprland";
  };

  outputs = { self, nixpkgs, ... }@inputs:
    let
      system = if builtins ? currentSystem
               then builtins.currentSystem
               else "x86_64-linux";
      pkgs = import nixpkgs { inherit system; };
      mkHomeCfg = { name, configuration, ...}:
        let
          username = "${builtins.head (builtins.match "(.+)@.+" name)}";
          homeDir = "/home/${username}";
        in
        inputs.home.lib.homeManagerConfiguration {
          inherit pkgs;
          modules = [
            inputs.hyprland.homeManagerModules.default
            configuration 
            ({...}:
            {
              home.username = username;
              home.homeDirectory = homeDir;
              home.stateVersion = "23.05";
            })
            ];
          extraSpecialArgs = { inherit inputs username homeDir; };
        };
      cfgs = {
        "steffen@necrophcodr-desktop" = true;
        "steffen@necrophcodr-netbook" = true;
        "steffen@win2" = true;
      };
    in
    {
      homeConfigurations = builtins.mapAttrs( name: value:
        mkHomeCfg {
          inherit name; 
          configuration = ./hosts/${builtins.head (builtins.match ".+@(.+)" name)}/home.nix; 
        }
      ) cfgs;
      nixosConfigurations = builtins.mapAttrs( name: value:
        nixpkgs.lib.nixosSystem {
          inherit system;
          modules = [ ./hosts/${name}/configuration.nix ];
          specialArgs = { inherit inputs; };
        }
      ) (builtins.listToAttrs( 
        builtins.map( value: 
          { 
            name = "${builtins.head (builtins.match ".+@(.+)" value)}"; 
            inherit value; 
          }) (builtins.attrNames cfgs) 
      ));
    };
}
